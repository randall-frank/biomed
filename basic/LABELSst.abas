 0  PRINT : PRINT "";"BRUN PRINT": VTAB 10: HTAB 6: PRINT "THE HEART:STRUCTURE AND OPERATION": VTAB 11: HTAB 9: PRINT "C 1983 BY RANDY FRANK": PRINT "";"BLOAD DIAGRAM"
 1  POKE 0,14: POKE 1,20: POKE 3,75: POKE 4,0
 2 C = 3: PRINT : PRINT "";"BLOAD MASSDRAW.OBJ0"
 3  PRINT CL$: CALL 768
 4  ONERR  GOTO 700
 100  VTAB 19: HTAB 4: PRINT "THE HEART IS THE CENTER OF THE        CIRCULATORY SYSTEM.IT IS LOCATED IN THE THORACIC CAVITY WHICH ALSO HOUSES THE   LUNGS."
 101  GOSUB 19000
 110  VTAB 19: HTAB 3: PRINT "THE BASIC ANATOMY OF THE HEART IS THE RIGHT ATRIUM,";:A = 1: GOSUB 19500
 111  VTAB 20: HTAB 14: PRINT "LEFT ATRIUM,":A = 2: GOSUB 19500: VTAB 20: HTAB 26: PRINT "RIGHT VENTRICLE,":A = 3: GOSUB 19500
 112  VTAB 21: HTAB 2: PRINT "AND LEFT VENTRICLE.";:A = 4: GOSUB 19500: GOSUB 19000
 120  VTAB 19: HTAB 3: PRINT "IN ADDITION,THE AORTA,":A = 5: GOSUB 19500: VTAB 19: HTAB 25: PRINT "PULMONARY ARTERY,":A = 6: GOSUB 19500
 121  VTAB 20: HTAB 2: PRINT "PULMONARY VEINS,":A = 7: GOSUB 19500: VTAB 20: HTAB 18: PRINT "SUPERIOR VENA CAVA,":A = 8: GOSUB 19500
 122  VTAB 20: HTAB 37: PRINT "AND INFERIOR VENA CAVA MAY BE SEEN.":A = 9: GOSUB 19500: GOSUB 19000
 130  PRINT CL$: POKE 0,14: POKE 1,20:A = 0: GOSUB 19900
 131  HTAB 2: VTAB 19: PRINT "IF WE CUT THE HEART IN HALF SO AS TO   SHOW THE INTERIOR WE WOULD FIND THAT IT LOOKS LIKE THIS."
 132  VTAB 16: PRINT "";"BRUN KNIFE"
 133  GOSUB 19000
 134  HCOLOR= 4: FOR T = 107 TO 116: HPLOT 40,T TO 200,T: NEXT T
 140 A = 1: GOSUB 19900
 141  VTAB 16: HTAB 3: PRINT "PRESS THE NUMBER CORRESPONDING TO THE     FOLLOWING PARTS OF THE HEART."
 150  FOR X = 0 TO 8: READ C$:V =  INT (X / 2)
 151  VTAB 18 + V: HTAB 1: IF  INT (X / 2) <  > X / 2 THEN  HTAB 21
 152  PRINT "?"; LEFT$ (C$,( LEN (C$) - 1));
 153  HTAB 1: IF  INT (X / 2) <  > X / 2 THEN  HTAB 21
 154  GET A$:A =  VAL (A$): PRINT A;
 155  IF A <  >  VAL ( RIGHT$ (C$,1)) THEN 151
 156  NEXT X
 157  GOSUB 19000: VTAB 16: PRINT  SPC( 120)
 160 A = 0: GOSUB 19900
 161  HTAB 4: VTAB 19: PRINT "ALSO VISIBLE ARE FOUR VALVES: THE    PULMONARY VALVE,":A = 3: GOSUB 19900: FOR T = 1 TO 40:C = 0: GOSUB 29030:C = 3: GOSUB 29030: NEXT T
 162 A = 2: GOSUB 19900: HTAB 17: VTAB 20: PRINT "THE TRICUSPID VALVE,": FOR T = 1 TO 40:C = 0: GOSUB 29040:C = 3: GOSUB 29040: NEXT T
 163 A = 4: GOSUB 19900: HTAB 37: VTAB 20: PRINT "THE MITRAL VALVE,": FOR T = 1 TO 40:C = 0: GOSUB 29050:C = 3: GOSUB 29050: NEXT T
 164 A = 5: GOSUB 19900: HTAB 14: VTAB 21: PRINT "AND THE AORTIC VALVE.": FOR T = 1 TO 40:C = 0: GOSUB 29060:C = 3: GOSUB 29060: NEXT T
 165 A = 0: GOSUB 19900: GOSUB 19000
 166  PRINT CL$: POKE 49168,1:A = 0: GOSUB 19900
 167  VTAB 19: HTAB 4: PRINT "THIS IS THE BEATING HEART. THE ARROWS SHOW THE FLOW OF BLOOD THROUGH THE      HEART. THE VALVES KEEP THE FLOW FROM   BEING REVERSED. PRESS 'RETURN' TO GO ON."
 168  FOR T = 1 TO 100: NEXT T:A = 6: GOSUB 19900: FOR T = 1 TO 100: NEXT T
 169 A = A + 1: GOSUB 19900: IF A = 9 THEN 168
 170  FOR T = 1 TO 100: NEXT T: IF  PEEK (49152) <  > 141 THEN 169
 171  HTAB 1: PRINT "";"BLOAD SYSTEM": VTAB 19: HTAB 1: PRINT  SPC( 160)
 172  PRINT CL$: HTAB 4: VTAB 19: PRINT "BLOOD NORMALLY FOLLOWS THE ABOVE     PATTERN. THE RED (SPOTTED) BLOOD IS     OXYGEN RICH. THE BLUE (PLAIN) BLOOD IS  FULL OF CARBON DIOXIDE."
 173  HTAB 9: VTAB 23: PRINT "PRESS 'RETURN' TO GO ON.":F = 0: POKE 49168,1
 174 A = 0: GOSUB 19900: FOR T = 1 TO 50: NEXT T: GOSUB 500
 175 A = A + 1: GOSUB 19900: FOR T = 1 TO 50: NEXT T: GOSUB 500: IF A = 10 THEN 174
 176  IF  PEEK (49152) <  > 141 THEN 175
 177  PRINT CL$: VTAB 2: HTAB 4: PRINT "WHY ARE THERE BLUE AND RED (SPOTTED)    AREAS?": VTAB 5: HTAB 4: PRINT "A.RED BLOOD IS HOT": VTAB 7: HTAB 4: PRINT "B.RED BLOOD IS OXYGENATED"
 178  VTAB 9: HTAB 4: PRINT "C.BLUE BLOOD INDICATES ROYALTY": VTAB 11: HTAB 4: PRINT "D.BLUE BLOOD IS OXYGENATED": VTAB 13: HTAB 5: PRINT "PRESS THE LETTER CORRESPONDING TO        THE CORRECT ANSWER."
 179  GET A$: IF A$ < "A" OR A$ > "D" THEN 179
 180  IF A$ = "B" THEN  VTAB 16: HTAB 5: PRINT "B IS CORRECT"
 181  IF A$ <  > "B" THEN  VTAB 16: HTAB 5: PRINT "SORRY B WAS THE CORRECT ANSWER"
 182  GOSUB 19000: PRINT CL$
 183  VTAB 1: HTAB 4: PRINT "TO REMEMBER THE DIFFERENCE BETWEEN   VEINS AND ARTERIES REMEMBER THIS,       ARTERIES AWAY AND VEINS TOWARD THE HEART"
 184  VTAB 6: HTAB 6: PRINT "PRESS (A)TERIES OR (V)EINS": VTAB 7: HTAB 2: PRINT "ARE THE BLUE REGIONS VEINS OR ARTERIES":V = 8:H = 8:R$ = "VA": GOSUB 600: VTAB 9: HTAB 2: PRINT "ARE THE RED REGIONS VEINS OR ARTERIES":V = 10:H = 8:R$ = "AV": GOSUB 600
 185  VTAB 11: HTAB 7: PRINT "PRESS (T)RUE OR (F)ALSE": VTAB 12: HTAB 2: PRINT "OXYGENATED BLOOD IS CARRIED IN THE ARTERIES":H = 10:V = 13:R$ = "TF": GOSUB 600
 186  HTAB 5: VTAB 15: PRINT "WHAT ABOUT THE PULMONARY VEINS? DO  THEY CARRY BLOOD (T)OWARD OR (A)WAY FROM THE HEART":V = 18:H = 10:R$ = "TA": GOSUB 600
 187  HTAB 5: VTAB 19: PRINT "THIS IS AN EXCEPTION TO THE RULE.   THE PULMONARY VEINS CARRY OXYGENATED    BLOOD TOWARD THE HEART.": GOSUB 19000
 188  PRINT CL$: VTAB 2: HTAB 7: PRINT "THE FLOW OF BLOOD THROUGH THE      HEART IS CONTROLLED BY AN ELECTRICAL   SYSTEM. THAT SYSTEM WILL BE EXPLORED    NEXT": GOSUB 19000
 190  PRINT : PRINT "";"RUN LABELS2"
 200  END 
 500 F =  NOT F:C$ = "    ":C1$ = "    ":C2$ = "    ": IF F THEN C$ = "HEAD":C1$ = "LUNG":C2$ = "BODY"
 510  VTAB 5: HTAB 27: PRINT C$: VTAB 9: HTAB 27: PRINT C1$: VTAB 17: HTAB 18: PRINT C2$: RETURN 
 600  VTAB V: HTAB H: PRINT "?": GET A$: IF A$ <  >  RIGHT$ (R$,1) AND A$ <  >  LEFT$ (R$,1) THEN 600
 610  IF A$ =  RIGHT$ (R$,1) THEN  VTAB V: HTAB H: PRINT "SORRY THE CORRECT ANSWER WAS "; LEFT$ (R$,1): RETURN 
 620  IF A$ =  LEFT$ (R$,1) THEN  VTAB V: HTAB H: PRINT  LEFT$ (R$,1);" IS CORRECT": RETURN 
 700  PRINT : PRINT "";"RUN HEART.MENU"
 19000  VTAB 23: HTAB 8: PRINT "PRESS 'RETURN' TO CONTINUE";: POKE  - 16368,1
 19001  IF  PEEK ( - 16384) <  > 141 THEN 19001
 19002  FOR T = 19 TO 23: VTAB T: HTAB 1: PRINT  SPC( 40): NEXT T: RETURN 
 19500  GOSUB 19900: FOR X = 1 TO 40:C = 0: GOSUB 20000:C = 3: GOSUB 20000: NEXT X:A = 0: GOSUB 19900: RETURN 
 19900  POKE 0,14: POKE 1,20: POKE 4,0: POKE 3,75 + A * 5: CALL 768: RETURN 
 19990  FOR X = 1 TO 9: FOR Y = 1 TO 10:C = 3:A = X: GOSUB 20000:C = 0: GOSUB 20000: NEXT Y:C = 3: GOSUB 20000: NEXT X
 19991  POKE 3,75: CALL 768: END 
 20000  REM 
 20001  ON A GOSUB 21000,22000,23000,24000,25000,26000,27000,28000,29000
 20002  RETURN 
 21000 C$ = "            ": IF C = 3 THEN C$ = "RIGHT ATRIUM"
 21001  HTAB 1: VTAB 9: PRINT C$
 21010  HCOLOR= C: HPLOT 86,68 TO 105,68
 21020  RETURN 
 22000 C$ = "           ": IF C = 3 THEN C$ = "LEFT ATRIUM"
 22001  HTAB 30: VTAB 10: PRINT C$
 22010  HCOLOR= C: HPLOT 201,75 TO 160,58
 22020  RETURN 
 23000 C$ = "               ": IF C = 3 THEN C$ = "RIGHT VENTRICLE"
 23001  HTAB 12: VTAB 16: PRINT C$
 23010  HCOLOR= C: HPLOT 133,120 TO 133,84
 23020  RETURN 
 24000 C$ = "    ":C1$ = "         ": IF C = 3 THEN C$ = "LEFT":C1$ = "VENTRICLE"
 24001  HTAB 31: VTAB 12: PRINT C$: HTAB 31: VTAB 13: PRINT C1$
 24010  HCOLOR= C: HPLOT 165,84 TO 207,96
 24020  RETURN 
 25000 C$ = "     ": IF C = 3 THEN C$ = "AORTA"
 25001  HTAB 18: VTAB 1: PRINT C$
 25010  HCOLOR= C: HPLOT 140,10 TO 140,34
 25020  RETURN 
 26000 C$ = "         ":C1$ = "      ": IF C = 3 THEN C$ = "PULMONARY":C1$ = "ARTERY"
 26001  HTAB 31: VTAB 4: PRINT C$: HTAB 31: VTAB 5: PRINT C1$
 26010  HCOLOR= C: HPLOT 207,32 TO 165,39
 26020  RETURN 
 27000 C$ = "         ":C1$ = "     ": IF C = 3 THEN C$ = "PULMONARY":C1$ = "VEINS"
 27001  HTAB 31: VTAB 7: PRINT C$: HTAB 31: VTAB 8: PRINT C1$
 27010  HCOLOR= C: HPLOT 207,56 TO 175,49
 27020  RETURN 
 28000 C$ = "        ":C1$ = "         ": IF C = 3 THEN C$ = "SUPERIOR":C1$ = "VENA CAVA"
 28001  HTAB 1: VTAB 4: PRINT C$: HTAB 1: VTAB 5: PRINT C1$
 28010  HCOLOR= C: HPLOT 65,32 TO 112,40
 28020  RETURN 
 29000 C$ = "        ":C1$ = "         ": IF C = 3 THEN C$ = "INFERIOR":C1$ = "VENA CAVA"
 29001  HTAB 1: VTAB 12: PRINT C$: HTAB 1: VTAB 13: PRINT C1$
 29010  HCOLOR= C: HPLOT 65,96 TO 112,96
 29020  RETURN 
 29030 C$ = "         ":C1$ = "     ": IF C = 3 THEN C$ = "PULMONARY":C1$ = "VALVE"
 29031  HTAB 1: VTAB 5: PRINT C$: HTAB 3: VTAB 6: PRINT C1$
 29032  HCOLOR= C: HPLOT 67,40 TO 140,54: RETURN 
 29040 C$ = "         ":C1$ = "     ": IF C = 3 THEN C$ = "TRICUSPID":C1$ = "VALVE"
 29041  HTAB 1: VTAB 8: PRINT C$: HTAB 3: VTAB 9: PRINT C1$
 29042  HCOLOR= C: HPLOT 67,64 TO 115,72: RETURN 
 29050 C$ = "      ":C1$ = "     ": IF C = 3 THEN C$ = "MITRAL":C1$ = "VALVE"
 29051  HTAB 32: VTAB 7: PRINT C$: HTAB 33: VTAB 8: PRINT C1$
 29052  HCOLOR= C: HPLOT 212,56 TO 160,64: RETURN 
 29060 C$ = "      ":C1$ = "     ": IF C = 3 THEN C$ = "AORTIC":C1$ = "VALVE"
 29061  HTAB 32: VTAB 11: PRINT C$: HTAB 33: VTAB 12: PRINT C1$
 29062  HCOLOR= C: HPLOT 212,88 TO 144,67: RETURN 
 29100  DATA  AORTA5,PULMONARY ARTERY6,LEFT ATRIUM2,RIGHT VENTRICLE3,SUPERIOR VENA CAVA8
 29110  DATA  RIGHT ATRIUM1,PULMONARY VEINS7,INFERIOR VENA CAVA9,LEFT VENTRICLE4
