 10 CL$ = CHR$(16)+CHR$(17)
 20 PRINT CL$
 30 VTAB 10: HTAB 6: PRINT "The heart:Structure and operation": PRINT CHR$(4);"BLOAD DIAGRAM": POKE 0,14: POKE 1,20: POKE 3,75: POKE 4,0:C = 3: PRINT : PRINT CHR$(4);"BLOAD MASSDRAW.OBJ0": PRINT CL$: CALL 768: ONERR  GOTO 700
 100  VTAB 19: HTAB 4: PRINT "The heart is the center of the        circulatory system.It is located in the thoracic cavity which also houses the   lungs.": GOSUB 19000
 110  VTAB 19: HTAB 3: PRINT "The basic anatomy of the heart is the right atrium,";:A = 1: GOSUB 19500: VTAB 20: HTAB 14: PRINT "left atrium,":A = 2: GOSUB 19500: VTAB 20: HTAB 26: PRINT "right ventricle,":A = 3: GOSUB 19500
 112  VTAB 21: HTAB 2: PRINT "and left ventricle.";:A = 4: GOSUB 19500: GOSUB 19000: VTAB 19: HTAB 3: PRINT "In addition,the aorta,":A = 5: GOSUB 19500: VTAB 19: HTAB 25: PRINT "pulmonary artery,":A = 6: GOSUB 19500
 121  VTAB 20: HTAB 2: PRINT "pulmonary veins,":A = 7: GOSUB 19500: VTAB 20: HTAB 18: PRINT "superior vena cava,":A = 8: GOSUB 19500: VTAB 20: HTAB 37: PRINT "and inferior vena cava may be seen.":A = 9: GOSUB 19500: GOSUB 19000
 130  PRINT CL$: POKE 0,14: POKE 1,20:A = 0: GOSUB 19900: HTAB 2: VTAB 19: PRINT "If we cut the heart in half so as to   show the interior we would find that it looks like this.": VTAB 16: PRINT CHR$(4);"BRUN KNIFE": GOSUB 19000
 134  HCOLOR= 4: FOR T = 107 TO 116: HPLOT 40,T TO 200,T: NEXT T:A = 1: GOSUB 19900: VTAB 16: HTAB 3: PRINT "Press the number corresponding to the     following parts of the heart."
 135  POKE  - 16368,1: FOR X = 0 TO 8: READ C$:V =  INT (X / 2)
 151  VTAB 18 + V: HTAB 1: IF  INT (X / 2) <  > X / 2 THEN  HTAB 23
 152  PRINT "?"; LEFT$ ("-" + C$,( LEN (C$)));: HTAB 1: IF  INT (X / 2) <  > X / 2 THEN  HTAB 23
 154  GET A$:A =  VAL (A$): PRINT A;: IF A <  >  VAL ( RIGHT$ (C$,1)) THEN  FOR QQ = 1 TO 50:QZ =  PEEK ( - 16336): NEXT QQ: GOTO 151
 156  NEXT X: GOSUB 19000: VTAB 16: PRINT  SPC( 120):A = 0: GOSUB 19900: HTAB 4: VTAB 19: PRINT "Also visible are four valves: the    pulmonary valve,":A = 3: GOSUB 19900: FOR T = 1 TO 40:C = 0: GOSUB 29030:C = 3: GOSUB 29030: NEXT T
 162 A = 2: GOSUB 19900: HTAB 17: VTAB 20: PRINT "the tricuspid valve,": FOR T = 1 TO 40:C = 0: GOSUB 29040:C = 3: GOSUB 29040: GOSUB 29030: NEXT T
 163 A = 4: GOSUB 19900: HTAB 37: VTAB 20: PRINT "the mitral valve,": FOR T = 1 TO 40:C = 0: GOSUB 29050:C = 3: GOSUB 29050: GOSUB 29030: GOSUB 29040: NEXT T
 164 A = 5: GOSUB 19900: HTAB 14: VTAB 21: PRINT "and the aortic valve.": FOR T = 1 TO 40:C = 0: GOSUB 29060:C = 3: GOSUB 29060: GOSUB 29050: GOSUB 29040: GOSUB 29030: NEXT T
 165 A = 0: GOSUB 19900: GOSUB 29030: GOSUB 29040: GOSUB 29050: GOSUB 29060: GOSUB 19000
 166  PRINT CL$: POKE 49168,1:A = 0: GOSUB 19900: VTAB 17: HTAB 4: PRINT "This is the beating heart. The arrows show the flow of blood through the      heart.";
 167  PRINT " The valves keep the flow from   being reversed. Note the opening and    closing of the valves and contractions  of the muscular walls of the heart.": PRINT "         Press 'RETURN' to continue.";
 168  FOR T = 1 TO 300: NEXT T:A = 6: GOSUB 19900: FOR T = 1 TO 300: NEXT T
 169 A = A + 1: GOSUB 19900: IF A = 9 THEN 168
 170  FOR T = 1 TO 300: NEXT T: IF  PEEK (49152) <  > 141 THEN 169
 171  PRINT CL$: PRINT : PRINT CHR$(4);"BLOAD SYSTEM"
 172  VTAB 19: HTAB 1: PRINT  SPC( 160): PRINT CL$: HTAB 4: VTAB 19: PRINT "Blood normally follows the above     pattern. The red (spotted) blood is     oxygen rich. The blue (plain) blood is  full of carbon dioxide."
 173  HTAB 9: VTAB 23: PRINT "Press 'RETURN' to continue.":F = 0: POKE 49168,1
 174 A = 0: GOSUB 19900: FOR T = 1 TO 50: NEXT T: GOSUB 500
 175 A = A + 1: GOSUB 19900: FOR T = 1 TO 50: NEXT T: GOSUB 500: IF A = 10 THEN 174
 176  IF  PEEK (49152) <  > 141 THEN 175
 177  PRINT CL$: VTAB 2: HTAB 4: PRINT "Why are there blue (plain) and red       (spotted) areas?": VTAB 5: HTAB 4: PRINT "A.Red blood is hot": VTAB 7: HTAB 4: PRINT "B.Red blood is oxygenated"
 178  VTAB 9: HTAB 4: PRINT "C.Blue blood indicates royalty": VTAB 11: HTAB 4: PRINT "D.Blue blood is oxygenated": VTAB 13: HTAB 5: PRINT "Press the letter corresponding to        the correct answer."
 179  GET A$: IF A$ < "A" OR A$ > "D" THEN  VTAB 16: HTAB 5: PRINT "A,B,C, or D please";: GOTO 179
 180  VTAB 16: HTAB 5: PRINT  SPC( 20);: IF A$ = "B" THEN  VTAB 16: HTAB 5: PRINT "B is correct"
 181  IF A$ <  > "B" THEN  VTAB 16: HTAB 5: PRINT "Sorry B was the correct answer"
 182  GOSUB 19000: PRINT CL$: VTAB 1: HTAB 4: PRINT "To remember the difference between   veins and arteries remember this,       Arteries away and veins toward the heart"
 183  VTAB 6: HTAB 6: PRINT "Press (A)teries or (V)eins": VTAB 7: HTAB 2: PRINT "Are the blue regions veins or arteries":V = 8:H = 8:R$ = "VA": GOSUB 600 
 184  VTAB 11: HTAB 2: PRINT "Are the red regions veins or arteries":V = 12:H = 8:R$ = "AV": GOSUB 600
 185  PRINT CL$: VTAB 11: HTAB 7: PRINT "Press (T)rue or (F)alse": VTAB 12: HTAB 2: PRINT "Oxygenated blood is carried in the ar-   teries":H = 12:V = 13:R$ = "TF": GOSUB 600: PRINT CL$
 186  HTAB 5: VTAB 12: PRINT "What about the pulmonary veins? do  they carry blood (T)oward OR (A)way from the heart":V = 15:H = 10:R$ = "TA": GOSUB 600
 187 HTAB 5: VTAB 19: PRINT "This is an exception to the rule.   The pulmonary veins carry oxygenated    blood toward the heart.": GOSUB 19000
 188  PRINT CL$: VTAB 2: HTAB 7: PRINT "The flow of blood through the      heart is controlled by an electrical   system. That system will be explored    next.": GOSUB 19000
 189 PRINT : PRINT CHR$(4);"RUN LABELS2": END 
 500 F =  NOT F:C$ = "    ":C1$ = "    ":C2$ = "    ": IF F THEN C$ = "Head":C1$ = "Lung":C2$ = "Body"
 510  VTAB 5: HTAB 27: PRINT C$: VTAB 9: HTAB 27: PRINT C1$: VTAB 17: HTAB 18: PRINT C2$: RETURN 
 600  VTAB V: HTAB H: PRINT "?": GET A$: IF A$ <  >  RIGHT$ (R$,1) AND A$ <  >  LEFT$ (R$,1) THEN 600
 610  IF A$ =  RIGHT$ (R$,1) THEN  VTAB V: HTAB H: PRINT "Sorry the correct answer was "; LEFT$ (R$,1): GOTO 19000
 620  IF A$ =  LEFT$ (R$,1) THEN  VTAB V: HTAB H: PRINT  LEFT$ (R$,1);" is correct": GOTO 19000
 700  PRINT : PRINT CHR$(4);"RUN HEART.MENU"
 19000  VTAB 23: HTAB 8: PRINT "Press 'RETURN' to continue";: POKE  - 16368,1
 19001  IF  PEEK ( - 16384) <  > 141 THEN 19001
 19002  FOR T = 19 TO 23: VTAB T: HTAB 1: PRINT  SPC( 40): NEXT T: RETURN 
 19500  GOSUB 19900: FOR X = 1 TO 100:C = 0: GOSUB 20000:C = 3: GOSUB 20000: NEXT X:A = 0: GOSUB 19900: RETURN 
 19900  POKE 0,14: POKE 1,20: POKE 4,0: POKE 3,75 + A * 5: CALL 768: RETURN 
 20000  REM 
 20001  ON A GOSUB 21000,22000,23000,24000,25000,26000,27000,28000,29000: RETURN 
 21000 C$ = "            ": IF C = 3 THEN C$ = "Right Atrium"
 21001  HTAB 1: VTAB 9: PRINT C$: HCOLOR= C: HPLOT 86,68 TO 105,68: RETURN 
 22000 C$ = "           ": IF C = 3 THEN C$ = "Left Atrium"
 22001  HTAB 30: VTAB 10: PRINT C$: HCOLOR= C: HPLOT 201,75 TO 160,58: RETURN 
 23000 C$ = "               ": IF C = 3 THEN C$ = "Right Ventricle"
 23001  HTAB 12: VTAB 16: PRINT C$: HCOLOR= C: HPLOT 133,120 TO 133,84: RETURN 
 24000 C$ = "    ":C1$ = "         ": IF C = 3 THEN C$ = "Left":C1$ = "Ventricle"
 24001  HTAB 31: VTAB 12: PRINT C$: HTAB 31: VTAB 13: PRINT C1$: HCOLOR= C: HPLOT 165,84 TO 207,96: RETURN 
 25000 C$ = "     ": IF C = 3 THEN C$ = "Aorta"
 25001  HTAB 18: VTAB 1: PRINT C$: HCOLOR= C: HPLOT 140,10 TO 140,34: RETURN 
 26000 C$ = "         ":C1$ = "      ": IF C = 3 THEN C$ = "Pulmonary":C1$ = "Artery"
 26001  HTAB 31: VTAB 4: PRINT C$: HTAB 31: VTAB 5: PRINT C1$: HCOLOR= C: HPLOT 207,32 TO 165,39: RETURN 
 27000 C$ = "         ":C1$ = "     ": IF C = 3 THEN C$ = "Pulmonary":C1$ = "Veins"
 27001  HTAB 31: VTAB 7: PRINT C$: HTAB 31: VTAB 8: PRINT C1$: HCOLOR= C: HPLOT 207,56 TO 175,49: RETURN 
 28000 C$ = "        ":C1$ = "         ": IF C = 3 THEN C$ = "Superior":C1$ = "Vena Cava"
 28001  HTAB 1: VTAB 4: PRINT C$: HTAB 1: VTAB 5: PRINT C1$: HCOLOR= C: HPLOT 65,32 TO 112,40: RETURN 
 29000 C$ = "        ":C1$ = "         ": IF C = 3 THEN C$ = "Inferior":C1$ = "Vena Cava"
 29001  HTAB 1: VTAB 12: PRINT C$: HTAB 1: VTAB 13: PRINT C1$: HCOLOR= C: HPLOT 65,96 TO 112,96: RETURN 
 29030 C$ = "         ":C1$ = "     ": IF C = 3 THEN C$ = "Pulmonary":C1$ = "Valve"
 29031  HTAB 1: VTAB 5: PRINT C$: HTAB 3: VTAB 6: PRINT C1$: HCOLOR= C: HPLOT 67,40 TO 140,54: RETURN 
 29040 C$ = "         ":C1$ = "     ": IF C = 3 THEN C$ = "Tricuspid":C1$ = "Valve"
 29041  HTAB 1: VTAB 8: PRINT C$: HTAB 3: VTAB 9: PRINT C1$: HCOLOR= C: HPLOT 67,64 TO 115,72: RETURN 
 29050 C$ = "      ":C1$ = "     ": IF C = 3 THEN C$ = "Mitral":C1$ = "Valve"
 29051  HTAB 32: VTAB 7: PRINT C$: HTAB 33: VTAB 8: PRINT C1$: HCOLOR= C: HPLOT 212,56 TO 160,64: RETURN 
 29060 C$ = "      ":C1$ = "     ": IF C = 3 THEN C$ = "Aortic":C1$ = "Valve"
 29061  HTAB 32: VTAB 11: PRINT C$: HTAB 33: VTAB 12: PRINT C1$: HCOLOR= C: HPLOT 212,88 TO 144,67: RETURN 
 29062 DATA   Pulmonary Artery6,Aorta5,Left Atrium2,Right Ventricle3,Superior Vena Cava8
 29063 DATA   Right Atrium1,Pulmonary Veins7,Left Ventricle4,Inferior Vena Cava9
